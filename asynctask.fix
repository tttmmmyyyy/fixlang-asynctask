// This module provides a way to run tasks asynchronously.
// 
// Importing this module automatically enables `--threaded` flag of the compiler. 
// Note that this causes some overhead even for a single-threaded program.
module AsyncTask;

// A type for a computation task that runs asynchronously.
type Task a = unbox struct { dtor : Destructor TaskHandle };

// A native handle of a task. This type is used only for implementation.
type TaskHandle = Ptr;

// Evaluate the boxed lazy pointer.
_run_task_function : Box (IO Ptr) -> IO Ptr;
_run_task_function = Box::@value;

FFI_EXPORT[_run_task_function, fixasynctask_run_task_function];

// Gets the result of a task.
// 
// This function blocks the current thread until the task is finished.
get : Task a -> a;
get = |task| task._get_io.unsafe_perform;

// Internal implementation of `Task::get` and `AsyncIOTask::get`.
_get_io : Task a -> IO a;
_get_io = |task| (
    task.@dtor.borrow_io(|task_handle|
        let task_result_ptr = *FFI_CALL_IO[Ptr fixasynctask_thread_get_task_result(Ptr), task_handle];
        let boxed = *task_result_ptr.boxed_from_retained_ptr;
        pure $ boxed.Box::@value
    )
);

// Makes a task which performs a computation asynchronously.
// 
// Example:
// ```
// module Main;
// import AsyncTask;
// 
// main : IO ();
// main = (
//     let sum_range = |from, to| (
//         loop((0, from), |(sum, i)| (
//             if i == to { break $ sum };
//             continue $ (sum + i, i + 1)
//         ))
//     );
//     let n = 1000000000;
//     // Compute the sum of numbers from 0 to n/2 - 1.
//     // This task will be executed asynchronously (if you are using multi-core CPU).
//     let sum_former = AsyncTask::make(|_| sum_range(0, n/2));
//     // Compute the sum of numbers from n/2 to n.
//     // We perfom this in the current thread while waiting for the result of the former task.
//     let sum_latter = sum_range(n/2, n);
//     // Sum up the results of the two computations.
//     let sum = sum_former.get + sum_latter;
//     // Then the sum should be n * (n - 1) / 2.
//     assert_eq(|_|"", sum, n * (n - 1) / 2);;
//     println $ 
//         "Sum of numbers from 0 to " + (n - 1).to_string + 
//         " is " + sum_former.get.to_string + " + " + sum_latter.to_string + 
//         " = " + sum.to_string + "."
// );
// ```
make : (() -> a) -> Task a;
make = |task| (
    let task = IO::from_runner $ |ios| (ios, task());
    _make_io(task).unsafe_perform
);

// Internal implementation of `Task::make` and `AsyncIOTask::make`.
_make_io : IO a -> IO (Task a);
_make_io = |task| (
    let lazy_box_a : Lazy (Box a) = |_| undefined("");

    // Create task function to be run on a thread.
    let task : IO Ptr = do {
        // Run the task function.
        let result = *task;
        // Return the boxed and threaded value.
        let boxed = mark_threaded(Box::make(result));
        boxed.boxed_to_retained_ptr
    };
    let task : Box (IO Ptr) = Box::make(task).mark_threaded;

    // Get the native handle of the task.
    let task_handle : TaskHandle = *FFI_CALL_IO[Ptr fixasynctask_thread_create_task(Ptr, Ptr, Ptr), 
        *task.boxed_to_retained_ptr,
        lazy_box_a.get_funptr_release,
        lazy_box_a.get_funptr_retain
    ];

    // In the destructor, release the task handle.
    let dtor = Destructor::make(task_handle, |task_handle| IO::from_runner $ |ios| (
        let (ios, _) = FFI_CALL_IOS[() fixasynctask_thread_release_task(Ptr), task_handle, ios];
        (ios, nullptr)
    ));
    Task { dtor : dtor }.pure
);

// Gets the number of processors (CPU cores) currently available.
// This is implemented by calling `sysconf(_SC_NPROCESSORS_ONLN)`.
// The runtime pools as many threads as this number to execute asynchronous tasks.
number_of_processors : I64;
number_of_processors = FFI_CALL[I64 fixasynctask_get_number_of_processors()];

namespace AsyncIOTask {

    // A type for an I/O action that can be run asynchronously.
    type IOTask a = unbox struct { _task : Task a };

    // Get the result of an asynchronous I/O action.
    get : IOTask a -> IO a;
    get = |task| task.@_task._get_io;

    // An `IO` version of `AsyncTask::make`.
    // 
    // Example:
    // ```
    // module Main;
    // import AsyncTask;
    // 
    // main : IO ();
    // main = (
    //     let print_ten : I64 -> IO () = |task_num| (
    //         loop_m(0, |i| (
    //             if i == 10 {
    //                 break_m $ ()
    //             } else {
    //                 let msg = "task number: " + task_num.to_string + ", i: " + i.to_string;
    //                 msg.println;;
    //                 continue_m $ i + 1
    //             }
    //         ))
    //     );
    //     (*AsyncIOTask::make(print_ten(0))).get;;
    //     (*AsyncIOTask::make(print_ten(1))).get;;
    //     pure()
    // );
    // ```
    make : IO a -> IO (IOTask a);
    make = |task| task._make_io.map(|task| IOTask { _task : task });
}

namespace Var {
    // A type of variable which can be modified from multiple threads.
    // 
    // ```
    // module Main;
    // import AsyncTask;
    // 
    // main : IO ();
    // main = (
    //     let logger = *Var::make([]); // A mutable array of strings.
    // 
    //     // Launch multiple threads, and log in which order each thread is executed.
    //     let num_threads = number_of_processors * 2;
    //     Iterator::range(0, num_threads).fold_m((), |i, _| (
    //         AsyncIOTask::make(
    //             logger.lock(|logs| (
    //                 let count = logs.get_size;
    //                 let msg = "Thread " + i.to_string + " is running at " + count.to_string + 
    //                     if count % 10 == 1 { "st" } else if count % 10 == 2 { "nd" } else if count % 10 == 3 { "rd" } else { "th" };
    //                 let msg = msg + if i == count { "." } else { "!" };
    //                 logger.set(logs.push_back(msg))
    //             ))
    //         )
    //     ));;
    //
    //     // Wait until all threads are finished.
    //     +logger.wait(|logs| logs.get_size == num_threads);
    // 
    //     println $ (*logger.get).to_iter.join("\n")
    // );
    // ```
    type Var a = unbox struct { _dtor : Destructor VarHandle };

    type VarHandle = Ptr;

    type VarValue a = box struct { value : a };

    // Get a value stored in a `Var`.
    get : Var a -> IO a;
    get = |Var { _dtor : dtor }| (
        dtor.borrow_io(|handle| 
            FFI_CALL_IO[() fixasynctask_thread_var_lock(Ptr), handle];;
            let ptr = *FFI_CALL_IO[Ptr fixasynctask_thread_var_get(Ptr), handle];
            FFI_CALL_IO[() fixasynctask_thread_var_unlock(Ptr), handle];;
            let boxed = *ptr.boxed_from_retained_ptr;
            pure $ boxed.VarValue::@value
        )
    );

    // `var.lock(act)` performs an action on the value in `var` while locking `var` to prevent it from being changed by another thread.
    lock : (a -> IO b) -> Var a -> IO b;
    lock = |act, Var { _dtor : dtor }| (
        dtor.borrow_io(|handle|
            FFI_CALL_IO[() fixasynctask_thread_var_lock(Ptr), handle];;
            let ptr = *FFI_CALL_IO[Ptr fixasynctask_thread_var_get(Ptr), handle];
            let boxed = *ptr.boxed_from_retained_ptr;
            let res = *act(boxed.VarValue::@value);
            FFI_CALL_IO[() fixasynctask_thread_var_unlock(Ptr), handle];;
            pure $ res
        )
    );

    // Create a new `Var` object.
    make : a -> IO (Var a);
    make = |value| (
        let value = mark_threaded(VarValue { value : value });
        let lazy_value = |_| value;
        let handle = *FFI_CALL_IO[Ptr fixasynctask_thread_var_create(Ptr, Ptr, Ptr), 
            *value.boxed_to_retained_ptr,
            lazy_value.get_funptr_release,
            lazy_value.get_funptr_retain
        ];
        let dtor = Destructor::make(handle, |handle| IO::from_runner $ |ios| (
            let (ios, _) = FFI_CALL_IOS[() fixasynctask_thread_var_destroy(Ptr), handle, ios];
            (ios, nullptr)
        ));
        Var { _dtor : dtor }.pure
    );

    // Atomically modifies a value in a `Var`.
    mod : (a -> a) -> Var a -> IO ();
    mod = |act, var| var.lock(|val| var.set(act(val)));

    // Set a value to a `Var`.
    set : a -> Var a -> IO ();
    set = |value, Var { _dtor : dtor }| (
        let value = mark_threaded(VarValue { value : value });
        let ptr = *value.boxed_to_retained_ptr;
        dtor.borrow_io(|handle|
            FFI_CALL_IO[() fixasynctask_thread_var_lock(Ptr), handle];;
            FFI_CALL_IO[() fixasynctask_thread_var_set(Ptr, Ptr), handle, ptr];;
            FFI_CALL_IO[() fixasynctask_thread_var_signalall(Ptr), handle];;
            FFI_CALL_IO[() fixasynctask_thread_var_unlock(Ptr), handle]
        )
    );

    // `var.wait(cond)` waits until `cond` on the value of `var` is satisfied.
    // 
    // Note that it is not assured that `cond` is satisfied after `wait` returned; 
    // the value in `var` may be changed after `cond` is evaluated.
    wait : (a -> Bool) -> Var a -> IO ();
    wait = |cond, var| var.wait_and_lock(cond, |_| pure());

    // `var.wait_and_lock(cond, act)` waits until `cond` on the value of `var` is satisfied, 
    // then performs `act` on the value in `var` while locking `var` to prevent it from being changed by another thread.
    wait_and_lock : (a -> Bool) -> (a -> IO b) -> Var a -> IO b;
    wait_and_lock = |cond, act, Var { _dtor : dtor }| (
        dtor.borrow_io(|handle|
            FFI_CALL_IO[() fixasynctask_thread_var_lock(Ptr), handle];;
            let ok_val = *loop_m((), |_| (
                let ptr = *FFI_CALL_IO[Ptr fixasynctask_thread_var_get(Ptr), handle];
                let boxed = *ptr.boxed_from_retained_ptr;
                let val = boxed.VarValue::@value;
                if cond(val) {
                    break_m $ val
                } else {
                    FFI_CALL_IO[() fixasynctask_thread_var_wait(Ptr), handle];;
                    continue_m $ ()
                }
            ));
            let res = *act(ok_val);
            FFI_CALL_IO[() fixasynctask_thread_var_unlock(Ptr), handle];;
            pure $ res
        )
    );
}